import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.jetbrains.grammarkit.tasks.*

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:1.1.3"
        classpath "gradle.plugin.org.jetbrains.intellij.plugins:gradle-grammarkit-plugin:2021.1.3"
    }
}

apply plugin: "java"
apply plugin: "org.jetbrains.grammarkit"
apply plugin: "org.jetbrains.intellij"

group = "com.mayreh.intellij.plugins"
version = "${version}" + (snapshot.toBoolean() ? "-SNAPSHOT" : "")

repositories {
    mavenCentral()
}

ext {
    lombokVersion = '1.18.22'
}

dependencies {
    implementation files('libs/tla2tools.jar')
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.assertj:assertj-core:3.21.0"
}

intellij {
    version = "211.7628.21"
    plugins = ["com.intellij.java", "org.intellij.intelliLang"]
    updateSinceUntilBuild = false
}

patchPluginXml {
    version = project.version
}

sourceSets {
    main {
        java.srcDirs("src/gen")
    }
}

task prepareLib(type: Exec) {
    workingDir project.rootDir
    commandLine './setup.sh'
}

task generateTLAplusLexer(type: GenerateLexer) {
    source = "src/main/grammer/tlaplus.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/tlaplus/lexer"
    targetClass = "_TLAplusLexer"
    purgeOldFiles = true
}

task generateTLAplusModuleBeginLexer(type: GenerateLexer) {
    source = "src/main/grammer/tlaplus_modulebegin.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/tlaplus/lexer"
    targetClass = "_TLAplusModuleBeginLexer"
    purgeOldFiles = true
}

task generateTLAplusPlusCalCommentLexer(type: GenerateLexer) {
    source = "src/main/grammer/tlaplus_plus_cal_comment.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/tlaplus/lexer"
    targetClass = "_TLAplusPlusCalCommentLexer"
    purgeOldFiles = true
}

task generateTLCErrorTraceLexer(type: GenerateLexer) {
    source = "src/main/grammer/tlc_error_trace.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/tlaplus/tlc/lexer"
    targetClass = "_TLCErrorTraceLexer"
    purgeOldFiles = true
}

task generateTLAplusParser(type: GenerateParser) {
    source = "src/main/grammer/tlaplus.bnf"
    targetRoot = "src/gen"
    pathToParser = "/com/mayreh/intellij/plugin/tlaplus/parser/TLAplusParser.java"
    pathToPsiRoot = "/com/mayreh/intellij/plugin/tlaplus/psi"
    purgeOldFiles = true
}

task generateTLCConfigParser(type: GenerateParser) {
    source = "src/main/grammer/tlc_config.bnf"
    targetRoot = "src/gen"
    pathToParser = "/com/mayreh/intellij/plugin/tlc/parser/TLCConfigParser.java"
    pathToPsiRoot = "/com/mayreh/intellij/plugin/tlc/psi"
    purgeOldFiles = true
}

task generateTLCConfigLexer(type: GenerateLexer) {
    source = "src/main/grammer/tlc_config.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/tlc/lexer"
    targetClass = "_TLCConfigLexer"
    purgeOldFiles = true
}

task generatePlusCalLexer(type: GenerateLexer) {
    source = "src/main/grammer/plus_cal.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/pluscal/lexer"
    targetClass = "_PlusCalLexer"
    purgeOldFiles = true
}

compileJava.dependsOn(
        prepareLib,
        generateTLAplusLexer,
        generateTLAplusModuleBeginLexer,
        generateTLAplusPlusCalCommentLexer,
        generateTLCErrorTraceLexer,
        generateTLAplusParser,
        generateTLCConfigParser,
        generateTLCConfigLexer,
        generatePlusCalLexer)

tasks.withType(Test) {
    testLogging {
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams false
    }
}

publishPlugin {
    token = findProperty("intellijPublishToken")
}
