import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.jetbrains.grammarkit.tasks.*

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:1.5.2"
        classpath "org.jetbrains.intellij.plugins:gradle-grammarkit-plugin:2021.2.2"
    }
}

apply plugin: "java"
apply plugin: "org.jetbrains.grammarkit"
apply plugin: "org.jetbrains.intellij"

group = "com.mayreh.intellij.plugins"
version = "${version}" + (snapshot.toBoolean() ? "-SNAPSHOT" : "")

repositories {
    mavenCentral()
}

ext {
    lombokVersion = '1.18.24'
}

dependencies {
    implementation files('libs/tla2tools.jar')
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.assertj:assertj-core:3.21.0"
}

intellij {
    version = "211.7628.21"
    plugins = ["org.intellij.intelliLang"]
    updateSinceUntilBuild = false
}

patchPluginXml {
    version = project.version
    sinceBuild = findProperty("sinceBuild")
    untilBuild = findProperty("untilBuild")
}

sourceSets {
    main {
        java.srcDirs("src/gen")
    }
}

task prepareLib(type: Exec) {
    workingDir project.rootDir
    commandLine './setup.sh'
}

task generateTLAplusLexer(type: GenerateLexerTask) {
    source = "src/main/grammar/tlaplus.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/tlaplus/lexer"
    targetClass = "_TLAplusLexer"
    purgeOldFiles = true
}

task generateTLAplusModuleBeginLexer(type: GenerateLexerTask) {
    source = "src/main/grammar/tlaplus_modulebegin.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/tlaplus/lexer"
    targetClass = "_TLAplusModuleBeginLexer"
    purgeOldFiles = true
}

task generateTLAplusPlusCalCommentLexer(type: GenerateLexerTask) {
    source = "src/main/grammar/tlaplus_plus_cal_comment.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/tlaplus/lexer"
    targetClass = "_TLAplusPlusCalCommentLexer"
    purgeOldFiles = true
}

task generateTLCErrorTraceLexer(type: GenerateLexerTask) {
    source = "src/main/grammar/tlc_error_trace.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/tlaplus/tlc/lexer"
    targetClass = "_TLCErrorTraceLexer"
    purgeOldFiles = true
}

task generateTLAplusParser(type: GenerateParserTask) {
    source = "src/main/grammar/tlaplus.bnf"
    targetRoot = "src/gen"
    pathToParser = "/com/mayreh/intellij/plugin/tlaplus/parser/TLAplusParser.java"
    pathToPsiRoot = "/com/mayreh/intellij/plugin/tlaplus/psi"
    purgeOldFiles = true
}

task generateTLCConfigParser(type: GenerateParserTask) {
    source = "src/main/grammar/tlc_config.bnf"
    targetRoot = "src/gen"
    pathToParser = "/com/mayreh/intellij/plugin/tlc/parser/TLCConfigParser.java"
    pathToPsiRoot = "/com/mayreh/intellij/plugin/tlc/psi"
    purgeOldFiles = true
}

task generateTLCConfigLexer(type: GenerateLexerTask) {
    source = "src/main/grammar/tlc_config.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/tlc/lexer"
    targetClass = "_TLCConfigLexer"
    purgeOldFiles = true
}

task generatePlusCalAlgorithmBeginLexer(type: GenerateLexerTask) {
    source = "src/main/grammar/plus_cal_algorithmbegin.flex"
    targetDir = "src/gen/com/mayreh/intellij/plugin/pluscal/lexer"
    targetClass = "_PlusCalAlgorithmBeginLexer"
    purgeOldFiles = true
}

compileJava.dependsOn(
        prepareLib,
        generateTLAplusLexer,
        generateTLAplusModuleBeginLexer,
        generateTLAplusPlusCalCommentLexer,
        generateTLCErrorTraceLexer,
        generateTLAplusParser,
        generateTLCConfigParser,
        generateTLCConfigLexer,
        generatePlusCalAlgorithmBeginLexer)

tasks.withType(Test) {
    testLogging {
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams false
    }
}

publishPlugin {
    token = findProperty("intellijPublishToken")
}
