import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.jetbrains.grammarkit.tasks.*

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:1.13.3"
        classpath "org.jetbrains.intellij.plugins:gradle-grammarkit-plugin:2022.3.1"
    }
}

apply plugin: "java"
apply plugin: "org.jetbrains.grammarkit"
apply plugin: "org.jetbrains.intellij"

group = "com.mayreh.intellij.plugins"
version = "${version}" + (snapshot.toBoolean() ? "-SNAPSHOT" : "")

repositories {
    mavenCentral()
}

ext {
    lombokVersion = '1.18.24'
}

dependencies {
    implementation files('libs/tla2tools.jar')
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.assertj:assertj-core:3.21.0"
}

intellij {
    version = findProperty("sinceBuild")
    plugins = ["org.intellij.intelliLang"]
    updateSinceUntilBuild = false
}

patchPluginXml {
    version = project.version
    sinceBuild = findProperty("sinceBuild")
    untilBuild = findProperty("untilBuild")
}

sourceSets {
    main {
        java.srcDirs("src/gen")
    }
}

task prepareLib(type: Exec) {
    workingDir project.rootDir
    commandLine './setup.sh'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

task generateTLAplusLexer(type: GenerateLexerTask) {
    sourceFile.set(file("src/main/grammar/tlaplus.flex"))
    targetDir.set("src/gen/com/mayreh/intellij/plugin/tlaplus/lexer")
    targetClass.set("_TLAplusLexer")
    purgeOldFiles.set(true)
}

task generateTLAplusModuleBeginLexer(type: GenerateLexerTask) {
    sourceFile.set(file("src/main/grammar/tlaplus_modulebegin.flex"))
    targetDir.set("src/gen/com/mayreh/intellij/plugin/tlaplus/lexer")
    targetClass.set("_TLAplusModuleBeginLexer")
    purgeOldFiles.set(true)
}

task generateTLAplusPlusCalCommentLexer(type: GenerateLexerTask) {
    sourceFile.set(file("src/main/grammar/tlaplus_plus_cal_comment.flex"))
    targetDir.set("src/gen/com/mayreh/intellij/plugin/tlaplus/lexer")
    targetClass.set("_TLAplusPlusCalCommentLexer")
    purgeOldFiles.set(true)
}

task generateTLCErrorTraceLexer(type: GenerateLexerTask) {
    sourceFile.set(file("src/main/grammar/tlc_error_trace.flex"))
    targetDir.set("src/gen/com/mayreh/intellij/plugin/tlaplus/tlc/lexer")
    targetClass.set("_TLCErrorTraceLexer")
    purgeOldFiles.set(true)
}

task generateTLAplusParser(type: GenerateParserTask) {
    sourceFile.set(file("src/main/grammar/tlaplus.bnf"))
    targetRoot.set("src/gen")
    pathToParser.set("/com/mayreh/intellij/plugin/tlaplus/parser/TLAplusParser.java")
    pathToPsiRoot.set("/com/mayreh/intellij/plugin/tlaplus/psi")
    purgeOldFiles.set(true)
}

task generateTLCConfigParser(type: GenerateParserTask) {
    sourceFile.set(file("src/main/grammar/tlc_config.bnf"))
    targetRoot.set("src/gen")
    pathToParser.set("/com/mayreh/intellij/plugin/tlc/parser/TLCConfigParser.java")
    pathToPsiRoot.set("/com/mayreh/intellij/plugin/tlc/psi")
    purgeOldFiles.set(true)
}

task generateTLCConfigLexer(type: GenerateLexerTask) {
    sourceFile.set(file("src/main/grammar/tlc_config.flex"))
    targetDir.set("src/gen/com/mayreh/intellij/plugin/tlc/lexer")
    targetClass.set("_TLCConfigLexer")
    purgeOldFiles.set(true)
}

task generatePlusCalAlgorithmBeginLexer(type: GenerateLexerTask) {
    sourceFile.set(file("src/main/grammar/plus_cal_algorithmbegin.flex"))
    targetDir.set("src/gen/com/mayreh/intellij/plugin/pluscal/lexer")
    targetClass.set("_PlusCalAlgorithmBeginLexer")
    purgeOldFiles.set(true)
}

compileJava.dependsOn(
        prepareLib,
        generateTLAplusLexer,
        generateTLAplusModuleBeginLexer,
        generateTLAplusPlusCalCommentLexer,
        generateTLCErrorTraceLexer,
        generateTLAplusParser,
        generateTLCConfigParser,
        generateTLCConfigLexer,
        generatePlusCalAlgorithmBeginLexer)

tasks.withType(Test) {
    testLogging {
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams false
    }
}

publishPlugin {
    token = findProperty("intellijPublishToken")
}
