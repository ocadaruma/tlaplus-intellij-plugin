{
    parserClass='com.mayreh.intellij.plugin.tlaplus.parser.TLAplusParser'
    parserUtilClass='com.mayreh.intellij.plugin.tlaplus.parser.TLAplusParserUtil'

//    implements='com.mayreh.intellij.plugin.tlaplus.psi.ext.TLAplusElement'
//    extends='com.mayreh.intellij.plugin.tlaplus.psi.ext.TLAplusElementImpl'

    elementTypeHolderClass='com.mayreh.intellij.plugin.tlaplus.psi.TLAplusElementTypes'

    elementTypeClass='com.mayreh.intellij.plugin.tlaplus.psi.TLAplusElementType'
    tokenTypeClass='com.mayreh.intellij.plugin.tlaplus.psi.TLAplusTokenType'

    psiClassPrefix='TLAplus'
    psiImplClassSuffix='Impl'
    psiPackage='com.mayreh.intellij.plugin.tlaplus.psi'
    psiImplPackage='com.mayreh.intellij.plugin.tlaplus.psi.impl'
    psiImplUtilClass='com.mayreh.intellij.plugin.tlaplus.psi.impl.TLAplusPsiImplUtil'

//    parserImports=[
//      'static com.mayreh.intellij.plugin.tlaplus.parser.TLAplusParserUtil.*'
//    ]

    tokens = [
        // outside of the module block
        IGNORED            = 'IGNORED'

        // tokens to parse disjunction
        JUNCTION_BEGIN            = 'JUNCTION_BEGIN'
        JUNCTION_CONT             = 'JUNCTION_CONT'
        JUNCTION_BREAK            = 'JUNCTION_BREAK'

        COMMENT_LINE       = 'COMMENT_LINE'
        COMMENT_BLOCK      = 'COMMENT_BLOCK'

        MODULE_BEGIN       = 'MODULE_BEGIN'

        KEYWORD_ASSUME     = 'ASSUME'
        KEYWORD_ELSE       = 'ELSE'
        KEYWORD_LOCAL      = 'LOCAL'
        KEYWORD_ASSUMPTION = 'ASSUMPTION'
        KEYWORD_MODULE     = 'MODULE'
        KEYWORD_VARIABLE   = 'VARIABLE'
        KEYWORD_AXIOM      = 'AXIOM'
        KEYWORD_EXCEPT     = 'EXCEPT'
        KEYWORD_OTHER      = 'OTHER'
        KEYWORD_VARIABLES  = 'VARIABLES'
        KEYWORD_CASE       = 'CASE'
        KEYWORD_EXTENDS    = 'EXTENDS'
        KEYWORD_SF_        = 'SF_'
        KEYWORD_WF_        = 'WF_'
        KEYWORD_CHOOSE     = 'CHOOSE'
        KEYWORD_IF         = 'IF'
        KEYWORD_WITH       = 'WITH'
        KEYWORD_CONSTANT   = 'CONSTANT'
        KEYWORD_IN         = 'IN'
        KEYWORD_THEN       = 'THEN'
        KEYWORD_CONSTANTS  = 'CONSTANTS'
        KEYWORD_INSTANCE   = 'INSTANCE'
        KEYWORD_THEOREM    = 'THEOREM'
        KEYWORD_LET        = 'LET'

        LITERAL_NUMBER     = 'number'
        LITERAL_STRING     = 'string'

        IDENTIFIER         = 'identifier'

        LTUPLE             = '<<'
        RTUPLE             = '>>'
        RTUPLEUNDER        = '>>_'
        COMMA              = ','
        SEPARATOR          = '----'
        MODULE_END         = '===='
        LPAREN             = '('
        RPAREN             = ')'
        LBRACKET           = '{'
        RBRACKET           = '}'
        LSQBRACKET         = '['
        RSQBRACKET         = ']'
        RSQBRACKETUNDER    = ']_'
        DOT                = '.'
        COLON              = ':'
        UNDER              = '_'
        BANG               = '!'
        AT                 = '@'

        DEFINE             = '=='

        OP_ENABLED         = 'ENABLED'
        OP_UNCHANGED       = 'UNCHANGED'
        OP_POWERSET        = 'SUBSET'
        OP_DOMAIN          = 'DOMAIN'
        OP_UNION           = 'UNION'
        OP_NEG             = '~'
        OP_SQUARE          = '[]'
        OP_DIAMOND         = '<>'
        OP_DASH            = '-'
        OP_LAND            = '\land'
        OP_LAND2           = '/\'
        OP_IN              = '\in'
        OP_LT              = '<'
        OP_LTEQ            = '=<'
        OP_LTLT            = '\ll'
        OP_PREC            = '\prec'
        OP_PRECEQ          = '\preceq'
        OP_SUBSETEQ        = '\subseteq'
        OP_SUBSET          = '\subset'
        OP_SQSUBSET        = '\sqsubset'
        OP_SQSUBSETEQ      = '\sqsubseteq'
        OP_VBARDASH        = '|-'
        OP_VBAREQ          = '|='
        OP_DASHGT          = '->'
        OP_CAP             = '\cap'
        OP_SQCAP           = '\sqcap'
        OP_OPLUS           = '(+)'
        OP_OMINUS          = '(-)'
        OP_ODOT            = '(.)'
        OP_OTIMES          = '(\X)'
        OP_OSLASH          = '(/)'
        OP_LOR             = '\lor'
        OP_LOR2            = '\/'
        OP_EQUIV           = '<=>'
        OP_NOTIN           = '\notin'
        OP_GT              = '>'
        OP_GTEQ            = '>='
        OP_GTGT            = '\gg'
        OP_SUCC            = '\succ'
        OP_SUCCEQ          = '\succeq'
        OP_SUPSETEQ        = '\supseteq'
        OP_SUPSET          = '\supset'
        OP_SQSUPSET        = '\sqsupset'
        OP_SQSUPSETEQ      = '\sqsupseteq'
        OP_DASHVBAR        = '-|'
        OP_EQVBAR          = '=|'
        OP_LTDASH          = '<-'
        OP_CUP             = '\cup'
        OP_SQCUP           = '\sqcup'
        OP_UPLUS           = '\uplus'
        OP_X               = '\X'
        OP_WR              = '\wr'
        OP_PROPTO          = '\propto'
        OP_EQGT            = '=>'
        OP_EQ              = '='
        OP_NOTEQ           = '/='
        OP_TILDEGT         = '~>'
        OP_DASHPLUSDASHGT  = '-+->'
        OP_VBARDASHGT      = '|->'
        OP_DIV             = '\div'
        OP_CDOT            = '\cdot'
        OP_CIRC            = '\circ'
        OP_BULLET          = '\bullet'
        OP_STAR            = '\star'
        OP_BIGCIRC         = '\bigcirc'
        OP_SIM             = '\sim'
        OP_SIMEQ           = '\simeq'
        OP_ASYMP           = '\asymp'
        OP_APPROX          = '\approx'
        OP_CONG            = '\cong'
        OP_DOTEQ           = '\doteq'
        OP_BANGBANG        = '!!'
        OP_SHARPSHARP      = '##'
        OP_DOLLAR          = '$'
        OP_DOLLARDOLLAR    = '$$'
        OP_PERCENT         = '%'
        OP_PERCENTPERCENT  = '%%'
        OP_AMP             = '&'
        OP_AMPAMP          = '&&'
        OP_ASTER           = '*'
        OP_ASTERASTER      = '**'
        OP_PLUS            = '+'
        OP_PLUSPLUS        = '++'
        OP_DASHDASH        = '--'
        OP_DOTDOT          = '..'
        OP_DOTDOTDOT       = '...'
        OP_SLASH           = '/'
        OP_SLASHSLASH      = '//'
        OP_COLONCOLONEQ    = '::='
        OP_COLONEQ         = ':='
        OP_COLONGT         = ':>'
        OP_LTCOLON         = '<:'
        OP_QUERY           = '?'
        OP_QUERYQUERY      = '??'
        OP_SUBTRACT        = '\'
        OP_CARET           = '^'
        OP_CARETCARET      = '^^'
        OP_VBAR            = '|'
        OP_VBARVBAR        = '||'
        OP_ATAT            = '@@'
        OP_PRIME           = "'"
        OP_CARETPLUS       = '^+'
        OP_CARETASTER      = '^*'
        OP_CARETSHARP      = '^#'
        OP_DASHDOT         = '-.' // only used when define '-' prefix operator

        QUANTIFIER_E       = '\E'
        QUANTIFIER_EE      = '\EE'
        QUANTIFIER_A       = '\A'
        QUANTIFIER_AA      = '\AA'

        // only for live preview
//        WHITE_SPACE = 'regexp:(\s|\R)+'
    ]

    extends('.*_expr') = expr
    consumeTokenMethod('.*_expr') = "consumeTokenFast"
//    generateTokenAccessors=true
}

module ::= module_header ('EXTENDS' <<comma_list 'identifier'>>)? unit* clear_indent '===='
{
  pin = 1
}

private meta comma_list ::= <<param>>(clear_indent ',' <<param>>)*

private clear_indent ::= junc_break*

module_header ::= clear_indent 'MODULE_BEGIN' 'identifier' '----'
{
  implements = 'com.mayreh.intellij.plugin.tlaplus.psi.TLAplusNamedElement'
  mixin = 'com.mayreh.intellij.plugin.tlaplus.psi.impl.TLAplusNamedElementImpl'
}

private unit ::= variable_decl
               | constant_decl
               | clear_indent 'LOCAL' op_definition
               | op_definition
               | clear_indent 'LOCAL' func_definition
               | func_definition
               | clear_indent 'LOCAL' instance
               | instance
               | clear_indent 'LOCAL' module_definition
               | module_definition
               | assumption
               | theorem
               | module
               | clear_indent '----'

private identifier_or_tuple ::= clear_indent ('identifier' | '<<' <<comma_list 'identifier'>> '>>')

prefix_op ::= clear_indent (
              '-'
            | '~'
            | '[]'
            | '<>'
            | 'DOMAIN'
            | 'ENABLED'
            | 'SUBSET'
            | 'UNCHANGED'
            | 'UNION')

infix_op ::= clear_indent (
             '!'
           | '/='
           | '##'
           | '$'
           | '$$'
           | '%'
           | '%%'
           | '&'
           | '&&'
           | '(+)'
           | '(-)'
           | '(.)'
           | '(/)'
           | '(\X)'
           | '*'
           | '**'
           | '+'
           | '++'
           | '-'
           | '-+->'
           | '--'
           | '-|'
           | '..'
           | '...'
           | '/'
           | '//'
           | '\land'
           | '::='
           | ':='
           | ':>'
           | '<'
           | '<:'
           | '<=>'
           | '\notin'
           | '='
           | '=<'
           | '=>'
           | '=|'
           | '>'
           | '>='
           | '?'
           | '??'
           | '@@'
           | '\'
           | '\lor'
           | '^'
           | '^^'
           | '|'
           | '|-'
           | '|='
           | '||'
           | '~>'
           | op_period
           | '\approx'
           | '\sqsupseteq'
           | '\asymp'
           | '\gg'
           | '\star'
           | '\bigcirc'
           | '\in'
           | '\prec'
           | '\subset'
           | '\bullet'
           | '\cap'
           | '\preceq'
           | '\subseteq'
           | '\propto'
           | '\succ'
           | '\cdot'
           | '\sim'
           | '\succeq'
           | '\circ'
           | '\ll'
           | '\simeq'
           | '\supset'
           | '\cong'
           | '\sqcap'
           | '\supseteq'
           | '\cup'
           | '\sqcup'
           | '\div'
           | '\sqsubset'
           | OP_UPLUS  // we can't use token value here since it causes illegal unicode escape in generated source
           | '\doteq'
           | '\sqsubseteq'
           | '\wr'
           | '\sqsupset')

private infix_op_outer ::= infix_op | <<parseInfixOpLand>> | <<parseInfixOpLor>>

postfix_op ::= clear_indent (
               '^+'
             | '^*'
             | '^#'
             | "'")

op_period ::= '.'

private op_suite ::= prefix_op | infix_op | postfix_op | expr

op_args ::= clear_indent '(' <<comma_list op_suite>> clear_indent ')'

private no_op_extension ::= clear_indent 'identifier' op_args? ('!' no_op_extension)?

private reduced_expr ::= no_op_extension
                       | paren_expr
                       | set_expr
                       | functions_expr
                       | records_expr
                       | except_expr
                       | action_expr
                       | tuple_expr

op_decl ::= clear_indent 'identifier'
          | clear_indent 'identifier' clear_indent '(' <<comma_list (clear_indent '_')>> clear_indent ')'
          | prefix_op clear_indent '_'
          | clear_indent '_' infix_op_outer clear_indent '_'
          | clear_indent '_' postfix_op

variable_decl ::= clear_indent ('VARIABLE' | 'VARIABLES') <<comma_list 'identifier'>>

constant_decl ::= clear_indent ('CONSTANT' | 'CONSTANTS') <<comma_list op_decl>>

nonfix_lhs ::= clear_indent 'identifier' (
   clear_indent '(' <<comma_list (clear_indent 'identifier' | op_decl)>> clear_indent ')')?

prefix_op_lhs ::= clear_indent prefix_op clear_indent 'identifier'

dashdot_op_lhs ::= clear_indent '-.' clear_indent 'identifier'

infix_op_lhs ::= clear_indent 'identifier' infix_op_outer clear_indent 'identifier'

postfix_op_lhs ::= clear_indent 'identifier' postfix_op

op_definition ::= (infix_op_lhs
                 | dashdot_op_lhs
                 | prefix_op_lhs
                 | postfix_op_lhs
                 | nonfix_lhs) clear_indent '==' expr

func_definition ::= clear_indent 'identifier'
    clear_indent '[' <<comma_list quantifier_bound>> clear_indent ']' clear_indent '==' expr

private quantifier_bound ::= (clear_indent '<<' <<comma_list (clear_indent 'identifier')>>
    clear_indent '>>' | <<comma_list (clear_indent 'identifier')>>) clear_indent '\in' expr

instance ::= clear_indent 'INSTANCE' clear_indent 'identifier' (clear_indent 'WITH' <<comma_list substitution>>)?
{
  pin = 2
}

private substitution ::= (clear_indent 'identifier'
                        | prefix_op
                        | infix_op_outer
                        | postfix_op) clear_indent '<-' argument

private argument ::= expr
                   | general_prefix_op
                   | general_infix_op
                   | general_postfix_op

instance_prefix ::= (clear_indent 'identifier' (
    clear_indent '(' <<comma_list expr>> clear_indent ')')? clear_indent '!')+

private general_identifier ::= instance_prefix? clear_indent 'identifier'

private general_prefix_op ::= instance_prefix? prefix_op

private general_infix_op ::= instance_prefix? infix_op_outer

private general_postfix_op ::= instance_prefix? postfix_op

module_definition ::= nonfix_lhs clear_indent '==' instance

assumption ::= clear_indent ('ASSUME' | 'ASSUMPTION' | 'AXIOM') expr { pin = 2 }

theorem ::= clear_indent 'THEOREM' expr { pin = 2 }

identifier_expr ::= general_identifier

identifier_args_expr ::= general_identifier clear_indent '(' <<comma_list argument>> clear_indent ')'

prefix_op_expr ::= general_prefix_op expr

infix_op_expr ::= expr general_infix_op expr

postfix_op_expr ::= expr general_postfix_op

paren_expr ::= clear_indent '(' expr clear_indent ')' { pin = 2 }

temporal_quantifier_expr ::= clear_indent ('\AA' | '\EE') <<comma_list (clear_indent 'identifier')>> clear_indent ':' expr
{
  pin = 2
}

quantifier_expr ::= clear_indent ('\A' | '\E') (<<comma_list quantifier_bound>>
                                              | <<comma_list (clear_indent 'identifier')>>) clear_indent ':' expr
{
  pin = 2
}

choose_expr ::= clear_indent 'CHOOSE' identifier_or_tuple ((clear_indent '\in' expr)? clear_indent ':' expr)
{
  pin = 2
}

set_expr ::= clear_indent '{' <<comma_list expr>>? clear_indent '}'
           | clear_indent '{' identifier_or_tuple clear_indent '\in' expr clear_indent ':' expr clear_indent '}'
           | clear_indent '{' expr clear_indent ':' <<comma_list quantifier_bound>> clear_indent '}'

apply_argument_expr ::= expr clear_indent '[' <<comma_list expr>> clear_indent ']'

functions_expr ::= clear_indent '[' <<comma_list quantifier_bound>> clear_indent '|->' expr clear_indent ']'
                 | clear_indent '[' expr clear_indent '->' expr clear_indent ']'

records_expr ::= clear_indent '[' <<comma_list (clear_indent 'identifier' clear_indent '|->' expr)>> clear_indent ']'
               | clear_indent '[' <<comma_list (clear_indent 'identifier' clear_indent ':' expr)>> clear_indent ']'

except_expr ::= clear_indent '[' expr clear_indent 'EXCEPT'
  <<comma_list (clear_indent '!' ((clear_indent '.' clear_indent 'identifier')
                                 | clear_indent '[' <<comma_list expr>> clear_indent ']')+ clear_indent '=' expr)
  >> clear_indent ']'

tuple_expr ::= clear_indent '<<' <<comma_list expr>> clear_indent '>>' { pin = 2 }

cartesian_product_expr ::= expr (clear_indent '\X' expr)+

action_expr ::= clear_indent '[' expr clear_indent ']_' reduced_expr
              | clear_indent '<<' expr clear_indent '>>_' reduced_expr

fairness_expr ::= clear_indent ('WF_' | 'SF_') reduced_expr (clear_indent '(' expr clear_indent ')')?
{
  pin = 2
}

if_expr ::= clear_indent 'IF' expr clear_indent 'THEN' expr clear_indent 'ELSE' expr
{
  pin = 2
}

private case_arm ::= expr clear_indent '->' expr

case_expr ::= clear_indent 'CASE' case_arm (clear_indent '[]' case_arm)*
    (clear_indent '[]' clear_indent 'OTHER' clear_indent '->' expr)?
{
  pin = 2
}

let_expr ::= clear_indent 'LET' (op_definition | func_definition | module_definition)+ clear_indent 'IN' expr
{
  pin = 2
}

private junc_begin ::= 'JUNCTION_BEGIN' {
  consumeTokenMethod = "consumeTokenFast"
}

private junc_cont ::= 'JUNCTION_CONT' {
  consumeTokenMethod = "consumeTokenFast"
}

private junc_break ::= 'JUNCTION_BREAK' {
  consumeTokenMethod = "consumeTokenFast"
}

// Junction-arm can be terminated by other tokens than JUNCTION_BREAK like braces, parenthesis
junction_list_expr ::= (junc_begin '/\' expr (junc_cont '/\' expr)* junc_break?)
                     | (junc_begin '\/' expr (junc_cont '\/' expr)* junc_break?)

literal_expr ::= clear_indent ('string' | 'number')

at_expr ::= clear_indent '@'

expr ::= if_expr
       | except_expr
       | tuple_expr
       | case_expr
       | quantifier_expr
       | temporal_quantifier_expr
       | choose_expr
       | let_expr
       | paren_expr
       | set_expr
       | functions_expr
       | records_expr
       | infix_op_expr
       | cartesian_product_expr
       | prefix_op_expr
       | postfix_op_expr
       | at_expr
       | junction_list_expr
       | action_expr
       | fairness_expr
       | apply_argument_expr
       | identifier_args_expr
       | identifier_expr
       | literal_expr
